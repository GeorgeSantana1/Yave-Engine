cmake_minimum_required(VERSION 3.6)
project(yave)

function(enable_unity_build UB_SUFFIX SOURCE_VARIABLE_NAME)
	set(FILES ${${SOURCE_VARIABLE_NAME}})

	# Exclude all translation units from compilation
	set_source_files_properties(${FILES} PROPERTIES HEADER_FILE_ONLY true)

	# Generate a unique filename for the unity build translation unit
	set(UNIT_BUILD_FILE ${CMAKE_CURRENT_BINARY_DIR}/blob_${UB_SUFFIX}.cpp)
	file(WRITE ${UNIT_BUILD_FILE} "// Unity Build generated by CMake\n")

	# Add include statement for each translation unit
	foreach(SOURCE_FILE ${FILES})
		get_filename_component(FILE_EXT "${SOURCE_FILE}" EXT)
		if(NOT "${FILE_EXT}" MATCHES "\.h(pp)?")
			file(APPEND ${UNIT_BUILD_FILE} "#include <${SOURCE_FILE}>\n")
		endif()
	endforeach(SOURCE_FILE)

	# Complement list of translation units with the name of ub
	set(${SOURCE_VARIABLE_NAME} ${${SOURCE_VARIABLE_NAME}} ${UNIT_BUILD_FILE} PARENT_SCOPE)
endfunction(enable_unity_build)

find_package(vulkan REQUIRED)

option(BUILD_YAVE "Build yave" ON)
option(BUILD_TOOLS "Build tools" ON)
option(BUILD_EDITOR "Build editor" ON)

# add projects
add_subdirectory(y)
add_subdirectory(yave)

if(BUILD_TOOLS)
	add_subdirectory(tools)
endif()

if(BUILD_EDITOR)
	add_subdirectory(editor)
endif()
